
##
# This prototypes object are sequenctially rendered as strings
# and output to form the the html response.
#
# For all the base data, see Neos.Neos:Page
##
prototype(Era.Site:Document.AbstractPage) < prototype(Neos.Neos:Page) {
    httpResponseHead {
        headers {
            // avoid click-jacking attacks
            // https://developer.mozilla.org/en-US/docs/Web/HTTP/Headers/X-Frame-Options
            'X-Frame-Options' = 'sameorigin'
            'X-XSS-Protection' = '1; mode=block'

            // configure from where things can be loaded
            // https://developer.mozilla.org/en-US/docs/Web/HTTP/CSP
            // 'Content-Security-Policy' = "default-src 'self' * 'unsafe-inline' 'unsafe-eval' data: https: blob:; report-uri /csp-violation-report/"

            // declared header content-type should be used, instead of sniffing a matching mime type
            // https://developer.mozilla.org/en-US/docs/Web/HTTP/Headers/X-Content-Type-Options
            'X-Content-Type-Options' = 'nosniff'

            // tell browsers that it should only be accessed using HTTPS, instead of using HTTP
            // https://developer.mozilla.org/en-US/docs/Web/HTTP/Headers/Strict-Transport-Security
            // 'Strict-Transport-Security' = 'max-age=63072000; includeSubDomains; preload'

            // Prevent Chrome from tracking users, see https://cleanuptheweb.org/
            Permissions-Policy = 'interest-cohort=()'
        }
    }

    headTags = Neos.Fusion:Component {
        @position = 'before closingHeadTag'

        renderer = afx`
            <meta http-equiv="x-ua-compatible" content="ie=edge" />
        

    <meta charset="utf-8" />
    <meta name="viewport" content="width=device-width, initial-scale=1, shrink-to-fit=no" />
    <!-- Google Fonts -->
    <link href="https://fonts.googleapis.com/css?family=Roboto:100,300,400,500,700" rel="stylesheet" />
    <!-- Bootstrap CSS -->
    <link rel="stylesheet" href="https://stackpath.bootstrapcdn.com/bootstrap/4.1.3/css/bootstrap.min.css" />
    <!-- Font Awesome CSS -->
    <link rel="stylesheet" type="text/css" href="https://use.fontawesome.com/releases/v5.3.1/css/all.css" />
    <!-- Slider CSS -->
    <link rel="stylesheet" href="https://unpkg.com/swiper@8/swiper-bundle.min.css" />

            <!-- link your own css files here -->
            <link rel="stylesheet" href={StaticResource.uri('Era.Site', 'Public/Frontend/index.css')} />
        `
    }

    body = Era.Site:Component.Layout.Default {
        content = ''
        @process.normalize = CodeQ.UnicodeNormalizer:Normalizer
    }

    scriptTags = Neos.Fusion:Component {
        @position = 'before closingBodyTag'

        renderer = afx`

            <script src="https://ajax.googleapis.com/ajax/libs/jquery/3.3.1/jquery.min.js"></script>
            <script src="https://cdnjs.cloudflare.com/ajax/libs/popper.js/1.14.3/umd/popper.min.js"></script>
            <script src="https://stackpath.bootstrapcdn.com/bootstrap/4.1.3/js/bootstrap.min.js"></script>
            <script src="https://unpkg.com/swiper@8/swiper-bundle.min.js"></script>
            <script src={StaticResource.uri('Era.Site', 'Public/Frontend/index.js')}></script>
            <!--
            Sitegeist.Lazybones required lazysizes, we recommend to include
            lazysizes javascript from https://github.com/aFarkas/lazysizes 
            into your own fe-build and remove this section
            -->
            <script src={StaticResource.uri('Sitegeist.Lazybones', 'Public/JavaScript/lazysizes.min.js')}></script>
        `
    }

    lastVisitedNodeScript = false
}
